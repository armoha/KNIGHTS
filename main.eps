// @bm CONSTANTS
const L_ANYWHERE = $L("Anywhere");
const L_MAIN = $L("main");
const L_3X3 = $L("3x3");
const L_16x16 = $L("16x16");
const L_PLAYER = [4, 5, 6, 7, 8, 9];

const U_HERO = [61, 62, 64, 12, 60, 86];
const U_CHIMERA = 37;
const U_ANY = 229;
const U_KAKARU = 94;
const U_SCOURGE = 47;
const U_WEAPON = 129;
const U_ARMOR = 219;
const U_MARKER = 7; // P10 = WEAPON, P11 = ARMOR

const ORDER_HOLD = 107;
const ORDER_PATROL = 152;

const FULL_DAY_LENGTH = 8640;
const BRIGHTNESS_DEFAULT = 31;
const BRIGHTNESS_NIGHT = 25;
const ELEVATION_DEFAULT = 4;
const ACTIVE_UNITS_MAX = 40;
const ACTIVE_UNITS_RECT = 960;
const MAX_MONSTER_SPAWNS = 1000;
const MAX_SIGIL_SPAWNS = 10;
const SIGIL_DIAMETER = 128;
const CLOSE_LOOT_MENU_DISTANCE = 64;

const MENU_MAIN_SIGIL = 1;

const PLAYER_DEFAULT_HP = 100;
const PLAYER_DEFAULT_SIGHT = 8;
const PLAYER_DEFAULT_SHIELD = 0;
const PLAYER_DEFAULT_DAMAGE = 6;
const PLAYER_DEFAULT_ATTRIBUTE = 1;

const WEAPON_SHORTSWORD = 1;
const WEAPON_SHORTBOW = 2;
const WEAPON_FIREBALL = 3;
const ARMOR_SHATTERED_KNIGHTS = 1;

const CHIMERA_EXP = 30;

const keypress_1 = PVariable();
const keypress_2 = PVariable();
const keypress_3 = PVariable();
const keypress_4 = PVariable();
const keypress_5 = PVariable();
const keypress_6 = PVariable();
const keypress_7 = PVariable();
const keypress_8 = PVariable();
const keypress_9 = PVariable();
const keypress_0 = PVariable();
const keypress_f = PVariable();
EUDRegisterObjectToNamespace("key_1", keypress_1);
EUDRegisterObjectToNamespace("key_2", keypress_2);
EUDRegisterObjectToNamespace("key_3", keypress_3);
EUDRegisterObjectToNamespace("key_4", keypress_4);
EUDRegisterObjectToNamespace("key_5", keypress_5);
EUDRegisterObjectToNamespace("key_6", keypress_6);
EUDRegisterObjectToNamespace("key_7", keypress_7);
EUDRegisterObjectToNamespace("key_8", keypress_8);
EUDRegisterObjectToNamespace("key_9", keypress_9);
EUDRegisterObjectToNamespace("key_0", keypress_0);
EUDRegisterObjectToNamespace("key_f", keypress_f);

const WEAPON_ARRAY_SIZE = 10;
const strings_weapons = EUDArray(WEAPON_ARRAY_SIZE);
const strings_weapon_descriptions = EUDArray(WEAPON_ARRAY_SIZE);

const strings_loot_interactions = EUDArray(10);

const p_cunit = PVariable(); // holds the cunit pointer of the players unit
const p_x = PVariable(); // coordinates of the unit
const p_y = PVariable();
const p_x_old = PVariable(); 
const p_y_old = PVariable();
const p_angle = PVariable(); // facing angle
const p_orderx = PVariable();
const p_ordery = PVariable();
const p_chunk_x_old = PVariable(); 
const p_chunk_y_old = PVariable();
const p_health = PVariable();
const p_health_max = PVariable();
const p_shield = PVariable();
const p_shield_max = PVariable();
const p_shield_recover = PVariable();
const p_shield_broken = PVariable();
const p_damage = PVariable(); // damage of the unit attack
const p_brightness = PVariable();
const p_exp_current = PVariable();
const p_exp_buffer = PVariable();
const p_exp_goal = PVariable();
const p_exp_total = PVariable();
const p_level_current = PVariable();
const p_armor = PVariable();
const p_homepos_x = PVariable();
const p_homepos_y = PVariable();
const p_indoors = PVariable();
const p_strength = PVariable();
const p_dexterity = PVariable();
const p_resilience = PVariable();
const p_attributepoints = PVariable();
const p_on_sigil = PVariable();
const p_current_sigil = PVariable();
const p_current_item_unit = PVariable();
const p_just_attacked = PVariable();

const MAX_WEAPON_OBJECTS = 6 + 50;
const weapon_cunit = EUDArray(MAX_WEAPON_OBJECTS);
const weapon = EUDArray(MAX_WEAPON_OBJECTS);
const weapon_state = EUDArray(MAX_WEAPON_OBJECTS);
const weapon_cooldown = EUDArray(MAX_WEAPON_OBJECTS);
const weapon_angle = EUDArray(MAX_WEAPON_OBJECTS);
const weapon_x = EUDArray(MAX_WEAPON_OBJECTS);
const weapon_y = EUDArray(MAX_WEAPON_OBJECTS);

const m_main = PVariable();
const m_page = PVariable();

const screen = StringBuffer(256);
const monster_spawn_type = EUDArray(MAX_MONSTER_SPAWNS);
const monster_spawn_x = EUDArray(MAX_MONSTER_SPAWNS);
const monster_spawn_y = EUDArray(MAX_MONSTER_SPAWNS);
const sigil_x = EUDArray(MAX_SIGIL_SPAWNS);
const sigil_y = EUDArray(MAX_SIGIL_SPAWNS);
const sigil_glow = EUDArray(MAX_SIGIL_SPAWNS);
const chunk_spawns = EUDArray(256);
const chunk_buffer = EUDArray(8);
const active_units = EUDArray(ACTIVE_UNITS_MAX * 6);
var active_units_update = 0;
// @bm DEBUG MODE
var debug_enabled = 1; 
const debug_values = EUDArray(8);
var time = 0;
var total_time = 0;
var time_state = 0;
var time_state_changed = 0;
var days_elapsed = 0;
var test_delay = 0;


function isAlly(p: TrgPlayer, tgp: TrgPlayer) {
    const q, r = div(tgp, 4);
    const epd = EPD(0x58D634) + q + (3 * p);
    var bitmask;
    switch (r, 3) {
        case 0: bitmask = 3 << 0; break;
        case 1: bitmask = 3 << 8; break;
        case 2: bitmask = 3 << 16; break;
        case 3: bitmask = 3 << 24; break;
    }
    return l2v(MemoryXEPD(epd, AtLeast, 1, bitmask));
}

function PlaySong(id) {
    if (id == 0) PlayWAV("staredit/wav/song0.ogg");
}

function PlaySword() {
    const sound = Random(1, 4);
    if (sound == 1) PlayWAV("staredit/wav/sword1.ogg");
    else if (sound == 2) PlayWAV("staredit/wav/sword2.ogg");
    else if (sound == 3) PlayWAV("staredit/wav/sword3.ogg");
    else if (sound == 4) PlayWAV("staredit/wav/sword4.ogg");
}

function GetStringItemName(item_id, unitID) {
    if (unitID == U_WEAPON) {
        switch(item_id) {
            case 0: return Db("Nothing");
            case WEAPON_SHORTSWORD: return Db("Short Sword");
            case WEAPON_SHORTBOW: return Db("Short Bow");
            case WEAPON_FIREBALL: return Db("Tome Of Fireball");
	    }
    } else if (unitID == U_ARMOR) {
        switch(item_id) {
            case 0: return Db("Nothing");
            case ARMOR_SHATTERED_KNIGHTS: return Db("Shattered Knight's Armor");
        }
    }
}

function GetStringItemDescription(item_id, unitID) {
    if (unitID == U_WEAPON) {
        switch(item_id) {
            case 0: return Db("Nothing");
            case WEAPON_SHORTSWORD: return Db("Basic sword.");
            case WEAPON_SHORTBOW: return Db("Basic bow.");
            case WEAPON_FIREBALL: return Db("Classic magic spell :).");
	    }
    } else if (unitID == U_ARMOR) {
        switch(item_id) {
            case 0: return Db("Nothing");
            case ARMOR_SHATTERED_KNIGHTS: return Db("Some old armor from another life.");
        }
    }
}

function SetupStringArrays() {
    strings_weapons[WEAPON_SHORTSWORD] = Db("Short Sword");
    strings_weapons[WEAPON_SHORTBOW] = Db("Short Bow");
    strings_weapons[WEAPON_FIREBALL] = Db("Tome Of Fireball");

    strings_weapon_descriptions[WEAPON_SHORTSWORD] = Db("Basic sword.");
    strings_weapon_descriptions[WEAPON_SHORTBOW] = Db("Basic bow.");
    strings_weapon_descriptions[WEAPON_FIREBALL] = Db("Classic magic spell :).");

    strings_loot_interactions[0] = Db("\x03-<1>- \x04to Equip.");
    strings_loot_interactions[1] = Db("\x03-<1>- \x04to Equip and swap with: ");
}

function GetWholeAndFraction(value) {
    // takes in a value * 10000 and returns the whole numbers + the fraction separately.
    var new_value = value;
    const whole = value / 10000;
    new_value -= (10000 * whole);
    const fraction = new_value;
    return whole, fraction;
}

function GetHealthToughnessDeduction(playerID, damage_amount) {
    // every point of dexterity = 2% damage reduced to the damage_amount.
    const percent_deducted = (p_dexterity[playerID] * 2) * 100;
    const deduction = (damage_amount * percent_deducted) / 10000;
    return deduction;
}

function GetArmorToughnessDeduction(playerID, damage_amount) {
    // every point of resilience = 2% damage reduced to the damage_amount.
    const percent_deducted = (p_resilience[playerID] * 2) * 100;
    const deduction = (damage_amount * percent_deducted) / 10000;
    return deduction;
}

function GetAttackSpeed(playerID) {
    var value = p_dexterity[playerID];
    if (value > 20) value = 20;
    return value;
}

function CalculateKnightSwordDamage(playerID) {
    p_damage[playerID] = PLAYER_DEFAULT_DAMAGE + (p_strength[playerID] * 3);
}

function GetHealthRegen(playerID) {
    return 2 * (p_strength[playerID] / 2); // 2 per frame = 0.1875 per second
}

function GetWeaponCooldown(item_id) {
    if (item_id == WEAPON_SHORTSWORD) return 24;
    else if (item_id == WEAPON_SHORTBOW) return 24;
    else if (item_id == WEAPON_FIREBALL) return 48;
}

function GetWeaponDamage(item_id, state, playerID) { // in the future could make unit an argument so that can have different damage based on unitid
    var damage = 0;
    if (playerID >= 0 && playerID <= 5) {
        if (item_id == WEAPON_SHORTSWORD) damage = 6 + (1 * p_strength[playerID]);
        else if (item_id == WEAPON_FIREBALL) damage = 4 + (2 * p_resilience[playerID]);
    } else if (playerID == $P7) {
        if (item_id == WEAPON_SHORTSWORD) damage = 30;
        else if (item_id == WEAPON_FIREBALL) damage = 50;
    }
    return damage;
}

function CheckForWeaponTrigger(playerID, unit: CUnit, weapon_id) {
    var is_player = 0;
    var weapon_was_triggered = 0;
    if (playerID >= 0 && playerID <= 5) is_player = 1;
    if (weapon_id == WEAPON_SHORTSWORD) {
        if (is_player && p_just_attacked[playerID] > 0) weapon_was_triggered = 1;
        else if (playerID == $P7 && unit.unitID == U_CHIMERA && unit.groundWeaponCooldown == 7) weapon_was_triggered = 1;
    }
    else if (weapon_id == WEAPON_FIREBALL) {
        if ((is_player && unit.orderID == 14) || (is_player && unit.orderID == 10)) weapon_was_triggered = 1;
        else if (playerID == $P7 && unit.unitID == U_CHIMERA && unit.groundWeaponCooldown == 7) weapon_was_triggered = 1;
    }
    return weapon_was_triggered;
}

function AttachWeaponToUnit(unit_ptr, weapon_id) {
    for (var i = 6; i < MAX_WEAPON_OBJECTS; i++) {
        if (weapon[i] == 0) {
            weapon[i] = weapon_id;
            weapon_cunit[i] = unit_ptr;
            break;
        }
    }
}

function GetDistance(originx, originy, destx, desty) {
    const a = originx - destx;
    const b = originy - desty;
    const distance = sqrt( a*a + b*b );
    return distance;
}

function CenterLoc(location, x, y) {
    setloc(L_MAIN, x, y);
    MoveLocation(location, $U("Map Revealer"), $P12, L_MAIN);
}

function CenterLocHero(location, playerID) {
    MoveLocation(location, U_HERO[playerID], playerID, L_ANYWHERE);
}

function EnemiesAtLoc(location) {
    if (Bring(Foes, AtLeast, 1, U_ANY, location)) return 1;
    return 0;
}

function StandingOnSigil(positionx, positiony) {
    for (var i = 0; i < MAX_SIGIL_SPAWNS; i++) {
        if (sigil_x[i] > 0) {
            const dist = GetDistance(positionx, positiony, sigil_x[i], sigil_y[i]);
            if (dist <= 64) return i;
        }
    }
    return MAX_SIGIL_SPAWNS + 1;
}

function CreateCUnit(unitType, playerID, location) : CUnit {
	const newUnit = CUnit.from_read(EPD(0x628438));
	CreateUnit(1, unitType, location, playerID);
	return newUnit;
}

function CreateItemUnit(unitType, item_id, location) {
    const new_unit = CUnit(CreateCUnit(unitType, $P8, location));
    new_unit.energy = item_id;
    new_unit.shield = 1000;
}

function GetVision(playerID) {
    if (playerID == 0) RunAIScript("+Vi0");
    else if (playerID == 1) RunAIScript("+Vi1");
    else if (playerID == 2) RunAIScript("+Vi2");
    else if (playerID == 3) RunAIScript("+Vi3");
    else if (playerID == 4) RunAIScript("+Vi4");
    else if (playerID == 5) RunAIScript("+Vi5");
    else if (playerID == 6) RunAIScript("+Vi6");
    else if (playerID == 7) RunAIScript("+Vi7");
}

function LoseVision(playerID) {
    if (playerID == 0) RunAIScript("-Vi0");
    else if (playerID == 1) RunAIScript("-Vi1");
    else if (playerID == 2) RunAIScript("-Vi2");
    else if (playerID == 3) RunAIScript("-Vi3");
    else if (playerID == 4) RunAIScript("-Vi4");
    else if (playerID == 5) RunAIScript("-Vi5");
    else if (playerID == 6) RunAIScript("-Vi6");
    else if (playerID == 7) RunAIScript("-Vi7");
}

function GetLocationPoint(location) {
	const locTable = EPD(0x58DC4C);
	return dwread_epd(locTable + location * 5), dwread_epd(locTable + location * 5 + 1);
}

function CreateUnitDeathAtLocation(Unit, playerID, location) {
    CreateUnit(1, Unit, location, playerID);
    KillUnit(Unit, playerID);
}

function StoreSpawnPositions() {
    var monster_i = 0;
    var sigil_i = 0;
    foreach(unit : EUDLoopCUnit()) {
        if (unit.playerID == $P9) {
            if (unit.unitType == 215 && sigil_i < MAX_SIGIL_SPAWNS) {
                sigil_i++;
                sigil_x[sigil_i] = unit.posX;
                sigil_y[sigil_i] = unit.posY;
                unit.remove();
            } 
            else if (monster_i < MAX_MONSTER_SPAWNS) {
                monster_spawn_type[monster_i] = unit.unitType;
                monster_spawn_x[monster_i] = unit.posX;
                monster_spawn_y[monster_i] = unit.posY;
                unit.remove();
                monster_i++;
                if (monster_i > MAX_MONSTER_SPAWNS) {
                    DisplayTextAll("NOTICE: MAXIMUM SPAWN POSITIONS REACHED!");
                    break;
                }
            }

        }
    }
}

function GetSumOfActivePlayerBits() {
    var sum = 0;
    if (playerexist($P1)) sum += 1;
    if (playerexist($P2)) sum += 2;
    if (playerexist($P3)) sum += 4;
    if (playerexist($P4)) sum += 8;
    if (playerexist($P5)) sum += 16;
    if (playerexist($P6)) sum += 32;
    return sum;
}

function SpawnInitialItemUnits() {
    foreach(unit : EUDLoopCUnit()) {
        if (unit.playerID == $P10) {
            if (unit.unitType == U_MARKER) {
                const weapon_id = unit.hp / 256;
                setloc(L_MAIN, unit.posX, unit.posY);
                RemoveUnitAt(1, U_MARKER, L_MAIN, $P10);
                const new_unit = CUnit(CreateCUnit(U_WEAPON, $P8, L_MAIN));
                new_unit.energy = weapon_id;
                new_unit.shield = GetSumOfActivePlayerBits();
            }
        } else if (unit.playerID == $P11) {
            if (unit.unitType == U_MARKER) {
                const armor_id = unit.hp / 256;
                setloc(L_MAIN, unit.posX, unit.posY);
                RemoveUnitAt(1, U_MARKER, L_MAIN, $P11);
                const new_unit = CUnit(CreateCUnit(U_ARMOR, $P8, L_MAIN));
                new_unit.energy = armor_id;
                new_unit.shield = GetSumOfActivePlayerBits();
            }
        }
    }
}

function UnitItemHasBeenLooted(playerID, value) {
    if (value > 63) return 0;
    var check_bits = value;
    var p0, p1, p2, p3, p4, p5 = 0, 0, 0, 0, 0, 0;
    if (check_bits >= 32) {check_bits -= 32; p5 = 1;}
    if (check_bits >= 16) {check_bits -= 16; p4 = 1;}
    if (check_bits >= 8) {check_bits -= 8; p3 = 1;}
    if (check_bits >= 4) {check_bits -= 4; p2 = 1;}
    if (check_bits >= 2) {check_bits -= 2; p1 = 1;}
    if (check_bits >= 1) {check_bits -= 1; p0 = 1;}
    if (playerID == 0 && p0 > 0) return 0;
    else if (playerID == 1 && p1 > 0) return 0;
    else if (playerID == 2 && p2 > 0) return 0;
    else if (playerID == 3 && p3 > 0) return 0;
    else if (playerID == 4 && p4 > 0) return 0;
    else if (playerID == 5 && p5 > 0) return 0;
    return 1;
}

function ModifyItemLootedStatus(playerID, current_value) {
    if (current_value >= 1000) return 0; // return 0 if the item is non-shared
    var new_value = current_value;
    if (playerID == 0) new_value -= 1;
    else if (playerID == 1) new_value -= 2;
    else if (playerID == 2) new_value -= 4;
    else if (playerID == 3) new_value -= 8;
    else if (playerID == 4) new_value -= 16;
    else if (playerID == 5) new_value -= 32;
    return new_value; // if this returns 0 the item should be removed from the map
}

function GetChunkID(chunkX, chunkY) {
    // returns a value 1 -> 256, from coordinates 1 -> 256
    if (chunkX > 0 && chunkX <= 16 && chunkY > 0 && chunkY <= 16) {
        return (chunkY - 1) * 16 + chunkX;
    }
    return 0;
}

function GetChunkCoords(chunkID) {
    // returns coordinates 1 -> 256 from an id 1 -> 256
    var chunkx, chunky = 0, 0;
    if (chunkID > 16 && chunkID <= 256) {
        chunkx = ((chunkID - 1) % 16) + 1;
        chunky = ((chunkID - 1) / 16) + 1;
    } else if (chunkID > 0 && chunkID <= 16) {
        chunkx = chunkID;
        chunky = 1;
    }
    return chunkx, chunky;
}

function FillChunkBuffer(chunkX, chunkY) {
    // fills chunk buffer array with ids of the 8 surrounding chunks, ids are zero if not valid like at the edge of the map
    // starts top and goes clockwise.
    for (var i = 0; i < 8; i++) chunk_buffer[i] = 0;
    chunk_buffer[0] = GetChunkID(chunkX, chunkY - 1);
    chunk_buffer[1] = GetChunkID(chunkX + 1, chunkY - 1);
    chunk_buffer[2] = GetChunkID(chunkX + 1, chunkY);
    chunk_buffer[3] = GetChunkID(chunkX + 1, chunkY + 1);
    chunk_buffer[4] = GetChunkID(chunkX, chunkY + 1);
    chunk_buffer[5] = GetChunkID(chunkX - 1, chunkY + 1);
    chunk_buffer[6] = GetChunkID(chunkX - 1, chunkY);
    chunk_buffer[7] = GetChunkID(chunkX - 1, chunkY - 1);
}

function SpawnUnitsAtChunk(chunkX, chunkY) {
    const limit_left = ((chunkX - 1) * 16) * 32;
    const limit_right = limit_left + 512;
    const limit_top = ((chunkY - 1) * 16) * 32;
    const limit_bottom = limit_top + 512;
    for (var i = 0; i < 1000; i++) {
        if (monster_spawn_x[i] >= limit_left && monster_spawn_x[i] < limit_right && monster_spawn_y[i] >= limit_top && monster_spawn_y[i] < limit_bottom) {
            if (monster_spawn_type[i] > 0) {
                setloc(L_MAIN, monster_spawn_x[i], monster_spawn_y[i]);
                const new_unit = CreateCUnit(monster_spawn_type[i], $P7, L_MAIN);
            }
        }
    }
    chunk_spawns[GetChunkID(chunkX, chunkY) - 1] += 1;
}

function JustEnteredChunk(chunkX, chunkY) {
    FillChunkBuffer(chunkX, chunkY);
    // screen.printfAt(8, "{} | {} | {}", chunk_buffer[7], chunk_buffer[0], chunk_buffer[1]);
    // screen.printfAt(9, "{} | -- | {}", chunk_buffer[6], chunk_buffer[2]);
    // screen.printfAt(10, "{} | {} | {}", chunk_buffer[5], chunk_buffer[4], chunk_buffer[3]);
    for (var i = 0; i < 8; i++) {
        if (chunk_buffer[i] > 0 && chunk_spawns[chunk_buffer[i] - 1] == 0) {
            const x, y = GetChunkCoords(chunk_buffer[i]);
            SpawnUnitsAtChunk(x, y);
        }
    }
}

function GetChunkAtPos(positionx, positiony) {
    var chunk_pos_x = (positionx / 32) / 16;
    var chunk_pos_y = (positiony / 32) / 16;
    return chunk_pos_x + 1, chunk_pos_y + 1;
}

function IsPosWithinRect(positionx, positiony, left, right, top, bottom) {
    if (positionx >= left && positionx <= right && positiony >= top && positiony <= bottom) return 1;
    return 0;
}

function IsAlive(playerID) {
    if (Command(playerID, AtLeast, 1, U_HERO[playerID])) return 1;
    return 0;
}

function IsPosIndoors(positionx, positiony) {
    if (IsPosWithinRect(positionx, positiony, 3840, 4390, 1630, 2340)) return 1;
    if (IsPosWithinRect(positionx, positiony, 2870, 3580, 890, 1440)) return 1;
    if (IsPosWithinRect(positionx, positiony, 0, 1280, 1280, 2660)) return 1;
    if (IsPosWithinRect(positionx, positiony, 1281, 1890, 1470, 2150)) return 1;
    if (IsPosWithinRect(positionx, positiony, 1281, 1632, 2151, 2480)) return 1;
    if (IsPosWithinRect(positionx, positiony, 1632, 1696, 2270, 2370)) return 1;
    return 0;
}

function CreateImageSpriteAtLocation(playerID, location, imageID, drawFunc) {
    const new_script = 281;
    const prev_script = dwread(0x666778 + 34000 + imageID * 4);
    dwwrite(0x666778 + 34000 + imageID * 4, new_script); // change image iscript
    dwwrite(0x666778 + 14000 + imageID * 1, drawFunc); // change image drawfunc
	SetMemoryX(0x00666458, SetTo, imageID, 0x0000ffff); // change scanner image to input image
    CreateUnit(1, 33, location, playerID);
	RemoveUnit(33, playerID);
	SetMemoryX(0x00666458, SetTo, 546, 0x0000ffff); // revert scanner image
    dwwrite(0x666778 + 34000 + imageID * 4, prev_script); // revert image iscript
}

const included = EUDArray(6);
function RedeemUnitExp(playerID, unitType) {
    var amount_players_nearby = 0;
    var exp_to_gain = 0;
    if (unitType == U_CHIMERA) exp_to_gain += 30;
    for (var i = 0; i < 6; i++) {
        included[i] = 0;
        if (i != playerID && GetDistance(p_x[playerID], p_y[playerID], p_x[i], p_y[i]) <= 640) {
            amount_players_nearby += 1;
            included[i] = 1;
        }
    }
    if (amount_players_nearby > 0) {
        exp_to_gain = (exp_to_gain / (amount_players_nearby + 1)) + 1;
        for (var i = 0; i < 6; i++) {
            if (included[i] > 0) p_exp_buffer[i] += exp_to_gain;
        }
    }
    p_exp_buffer[playerID] += exp_to_gain;
}

function DamageUnit(playerID, unit: CUnit, damage, index) {
    // returns if the unit is dead or not
    var target_state = 1;
    var damage_remaining = damage * 256;
    if (unit.shield > 0) {
        if (unit.shield < damage_remaining) {
            damage_remaining -= unit.shield;
            unit.shield = 0;
        } else {
            unit.shield -= damage_remaining;
            damage_remaining = 0;
        }
    }
    if (unit.hp < damage_remaining) {
        unit.hp = 0;
    }
	if (unit.hp == 0) {
        if (unit.playerID == $P7 && index < ACTIVE_UNITS_MAX) active_units[index] = 0; // remove from active units array
        if (playerID >= 0 && playerID <= 5) RedeemUnitExp(playerID, unit.unitType);
        unit.die();
        target_state = 0;
    } else {
        unit.hp -= damage_remaining;
    }
    return target_state;
}

function HealPlayer(playerID, unit: CUnit, amount) {
    if (unit.hp == p_health_max[playerID]) return;
    else if (unit.hp + amount > p_health_max[playerID]) unit.hp = p_health_max[playerID];
    else unit.hp += amount;
}

function HealShieldPlayer(playerID, unit: CUnit, amount) {
    if (unit.shield == p_shield_max[playerID]) return;
    else if (unit.shield + amount > p_shield_max[playerID]) unit.shield = p_shield_max[playerID];
    else unit.shield += amount;
}

function SCDirectionToDeg(direction) {
    var angle = direction * 14063;
    angle = (angle/10000) - 90; // could make this number readable in the future by keeping it in the range of 0 - 359
    return angle;
}

function GetAngle(originx, originy, destinationx, destinationy) {
    const angle = atan2(destinationy - originy, destinationx - originx);
    return angle;
}

function AddAngles(angle0, angle1) {
    var new_angle = angle0 + angle1;
    if (new_angle > 4000000000) new_angle += 360;
    else if (new_angle >= 360) new_angle -= 360;
    return new_angle;
}

function MovePosTowards(positionx, positiony, angle, distance) {
    const directionx, directiony = lengthdir(distance, angle);
    const new_positionx = positionx + directionx;
    const new_positiony = positiony + directiony;
    return new_positionx, new_positiony;
}

function Random(min, max) {
   return min + dwrand() / (4294967295 / (max - min + 1) + 1);
}

function PosIsOnMap(positionx, positiony) {
    if (positionx >= 0 && positionx <= 8191 && positiony >= 0 && positiony <= 8191) return 1;
    return 0;
}

function PosToSquare(positionx, positiony, width) {
    const half_width = width/2;
    var limit_left = 0;
    if (positionx > half_width) limit_left = positionx - half_width;
    const limit_right = positionx + half_width;
    var limit_top = 0;
    if (positiony > half_width) limit_top = positiony - half_width;
    const limit_bottom = positiony + half_width;
    return limit_left, limit_right, limit_top, limit_bottom;
}

function GetSpawnPosition(playerID) {
    if (playerID == 0) return 1984, 1184;
    else if (playerID == 1) return 2048, 1120;
    else if (playerID == 2) return 2128, 1072;
    else if (playerID == 3) return 2192, 1120;
    else if (playerID == 4) return 2144, 1168;
    else if (playerID == 5) return 2080, 1200;
    return 0, 0;
}

function SpawnHero(playerID) {
    if (debug_enabled) p_homepos_x[playerID], p_homepos_y[playerID] = GetLocationPoint(L_PLAYER[playerID]);
    if (p_homepos_x[playerID] == 0) p_homepos_x[playerID], p_homepos_y[playerID] = GetSpawnPosition(playerID);
    if (IsAlive(playerID)) RemoveUnit(U_HERO[playerID], playerID);
    setloc(L_MAIN, p_homepos_x[playerID], p_homepos_y[playerID]);
    p_cunit[playerID] = CreateCUnit(U_HERO[playerID], playerID, L_MAIN);
    CenterView(L_MAIN);
    const unit = CUnit(p_cunit[playerID]);
    unit.killCount = p_level_current[playerID];
}

function SetMaxHP(playerID, value) {
    p_health[playerID] = value;
    p_health_max[playerID] = value;
    dwwrite(0x65FD00 + 9808 + U_HERO[playerID] * 4, value);
    ModifyUnitHitPoints(1, U_HERO[playerID], playerID, L_ANYWHERE, 100);
}

function CalculateMaxShield(playerID) {
    p_shield_max[playerID] = 0;
    if (p_armor[playerID] == ARMOR_SHATTERED_KNIGHTS) p_shield_max[playerID] += 100 * 256;
}

function EquipItem(item_id, unitType, playerID) {
    if (unitType == U_WEAPON) {
        weapon[playerID] = item_id;
    } else if (unitType == U_ARMOR) {
        p_armor[playerID] = item_id;
        CalculateMaxShield(playerID);
    }
}

function SetHeroDefaults(playerID) {
    p_exp_goal[playerID] = 150;
    p_shield_max[playerID] = PLAYER_DEFAULT_SHIELD * 256;
    p_damage[playerID] = PLAYER_DEFAULT_DAMAGE;
    p_attributepoints[playerID] = PLAYER_DEFAULT_ATTRIBUTE;
    SetMaxHP(playerID, PLAYER_DEFAULT_HP * 256);
}

function DrawCircle(positionx, positiony, diameter, dots, imageID) {
    var angle_increment = (3600000 / dots) / 10000;
    var angle = 0;
    for (var i = 0; i < dots; i++) {
        const x, y = MovePosTowards(positionx, positiony, angle, diameter / 2);
        setloc(L_MAIN, x, y);
        CreateImageSpriteAtLocation($P8, L_MAIN, imageID, 0);
        angle += angle_increment;
    }
}

function ClearTextLines(from_line, to_line) {
    for (var i = from_line; i < to_line + 1; i++) {
        DisplayTextAt(i, "\x0BNull");
    }
}

const PROJECTILE_POOL = 192;
const pro_posx = EUDArray(PROJECTILE_POOL);
const pro_posy = EUDArray(PROJECTILE_POOL);
const pro_time = EUDArray(PROJECTILE_POOL);
const pro_maxtime = EUDArray(PROJECTILE_POOL);
const pro_angle = EUDArray(PROJECTILE_POOL);
const pro_velocity = EUDArray(PROJECTILE_POOL);
const pro_angular_velocity = EUDArray(PROJECTILE_POOL);
const pro_orbit = EUDArray(PROJECTILE_POOL);
const pro_orbit_velocity = EUDArray(PROJECTILE_POOL);
const pro_orbit_dist = EUDArray(PROJECTILE_POOL);
const pro_effect0_type = EUDArray(PROJECTILE_POOL); //whether the effect is an image or a unit death
const pro_effect0 = EUDArray(PROJECTILE_POOL);
const pro_effect0_start = EUDArray(PROJECTILE_POOL);
const pro_effect1_type = EUDArray(PROJECTILE_POOL);
const pro_effect1 = EUDArray(PROJECTILE_POOL);
const pro_effect1_start = EUDArray(PROJECTILE_POOL);
const pro_effect2_type = EUDArray(PROJECTILE_POOL);
const pro_effect2 = EUDArray(PROJECTILE_POOL);
const pro_effect2_start = EUDArray(PROJECTILE_POOL);
function ResetProjectile(i) {
    pro_time[i] = 0;
    pro_maxtime[i] = 0;
    pro_posx[i] = 0;
    pro_posy[i] = 0;
    pro_angle[i] = 0;
    pro_velocity[i] = 0;
    pro_angular_velocity[i] = 0;
    pro_orbit[i] = 0;
    pro_orbit_velocity[i] = 0;
    pro_orbit_dist[i] = 0;
    pro_effect0_type[i] = 0;
    pro_effect0[i] = 0;
    pro_effect0_start[i] = 0;
    pro_effect1_type[i] = 0;
    pro_effect1[i] = 0;
    pro_effect1_start[i] = 0;
    pro_effect2_type[i] = 0;
    pro_effect2[i] = 0;
    pro_effect2_start[i] = 0;
}

var queue_pro_posx = 0;
var queue_pro_posy = 0;
var queue_pro_time = 0;
var queue_pro_maxtime = 0;
var queue_pro_angle = 0;
var queue_pro_velocity = 0;
var queue_pro_angular_velocity = 0;
var queue_pro_orbit = 0;
var queue_pro_orbit_velocity = 0;
var queue_pro_orbit_dist = 0;
var queue_pro_effect0_type = 0;
var queue_pro_effect0 = 0;
var queue_pro_effect0_start = 0;
var queue_pro_effect1_type = 0;
var queue_pro_effect1 = 0;
var queue_pro_effect1_start = 0;
var queue_pro_effect2_type = 0;
var queue_pro_effect2 = 0;
var queue_pro_effect2_start = 0;
function ResetProjectileQueue() {
    queue_pro_posx = 0;
    queue_pro_posy = 0;
    queue_pro_time = 0;
    queue_pro_maxtime = 0;
    queue_pro_angle = 0;
    queue_pro_velocity = 0;
    queue_pro_angular_velocity = 0;
    queue_pro_orbit = 0;
    queue_pro_orbit_velocity = 0;
    queue_pro_orbit_dist = 0;
    queue_pro_effect0_type = 0;
    queue_pro_effect0 = 0;
    queue_pro_effect0_start = 0;
    queue_pro_effect1_type = 0;
    queue_pro_effect1 = 0;
    queue_pro_effect1_start = 0;
    queue_pro_effect2_type = 0;
    queue_pro_effect2 = 0;
    queue_pro_effect2_start = 0;
}

function CreateProjectile() {
    for (var i = 0; i < PROJECTILE_POOL; i++) {
        if (pro_time[i] == 0) {
            pro_time[i] = 1;
            pro_maxtime[i] = queue_pro_maxtime;
            pro_posx[i] = queue_pro_posx;
            pro_posy[i] = queue_pro_posy;
            pro_angle[i] = queue_pro_angle;
            pro_velocity[i] = queue_pro_velocity;
            pro_angular_velocity[i] = queue_pro_angular_velocity;
            pro_orbit[i] = queue_pro_angle;
            pro_orbit_velocity[i] = queue_pro_orbit_velocity;
            pro_orbit_dist[i] = queue_pro_orbit_dist;
            pro_effect0_type[i] = queue_pro_effect0_type;
            pro_effect0[i] = queue_pro_effect0;
            pro_effect0_start[i] = queue_pro_effect0_start;
            pro_effect1_type[i] = queue_pro_effect1_type;
            pro_effect1[i] = queue_pro_effect1;
            pro_effect1_start[i] = queue_pro_effect1_start;
            pro_effect2_type[i] = queue_pro_effect2_type;
            pro_effect2[i] = queue_pro_effect2;
            pro_effect2_start[i] = queue_pro_effect2_start;
            ResetProjectileQueue();
            break;
        }
    }
}

function UpdateProjectiles() {
    for (var i = 0; i < PROJECTILE_POOL; i++) {
        if (pro_time[i] > 0) {
            pro_time[i] += 1;
            pro_angle[i] = AddAngles(pro_angle[i], pro_angular_velocity[i]);
            const new_posx, new_posy = MovePosTowards(pro_posx[i], pro_posy[i], pro_angle[i], pro_velocity[i]);
            pro_posx[i] = new_posx;
            pro_posy[i] = new_posy;

            pro_orbit[i] = AddAngles(pro_orbit[i], pro_orbit_velocity[i]);
            const offsetx, offsety = MovePosTowards(new_posx, new_posy, pro_orbit[i], pro_orbit_dist[i]);
            setloc(L_MAIN, offsetx, offsety);
            //IMAGES
            if (PosIsOnMap(offsetx, offsety) ) {
                if (pro_effect0[i] > 0 && pro_time[i] >= pro_effect0_start[i] && pro_time[i] < pro_effect1_start[i]) {
                    // time for effect 0
                    if (pro_effect0_type[i] == 0) CreateImageSpriteAtLocation($P8, L_MAIN, pro_effect0[i], 0);
                    else CreateUnitDeathAtLocation(pro_effect0[i], $P8, L_MAIN);
                } else if (pro_effect1[i] > 0 && pro_time[i] >= pro_effect1_start[i] && pro_time[i] < pro_effect2_start[i]) {
                    // time for effect 1
                    if (pro_effect1_type[i] == 0) CreateImageSpriteAtLocation($P8, L_MAIN, pro_effect1[i], 0);
                    else CreateUnitDeathAtLocation(pro_effect1[i], $P8, L_MAIN);
                } else if (pro_effect2[i] > 0 && pro_time[i] >= pro_effect2_start[i]) {
                    // time for effect 2
                    if (pro_effect2_type[i] == 0) CreateImageSpriteAtLocation($P8, L_MAIN, pro_effect2[i], 0);
                    else CreateUnitDeathAtLocation(pro_effect2[i], $P8, L_MAIN);
                }
            }
            if (pro_time[i] >= pro_maxtime[i]) {
                ResetProjectile(i);
            }
        }
    }
}

var col_posx = 0;
var col_posy = 0;
var col_enemies = 0;
var col_allies = 0;
var col_terrain = 0; // check if the posx, posy is unpathable.
var col_shape = 0; // determines if the check will be for circle or square. Shape only works for units. 
var col_shape_size = 0; // width of the square, or diameter of the circle.
var col_max_results = 0; // should probably increase max results in the future and make it a constant
const col_results_unit = EUDArray(8);
const col_results_unit_activei = EUDArray(8); // index position of the unit in the active units array, this is for deleting it from the active units array if it dies.
var col_results_unit_i = 0;
var col_results_terrain = 0;
function AddUnitToColResults(value, indexfromactive) {
    if (col_results_unit_i < col_max_results) {
        col_results_unit[col_results_unit_i] = value;
        col_results_unit_activei[col_results_unit_i] = indexfromactive;
        col_results_unit_i++;
        return 0;
    } else {
        return 1;
    } // returns 0 if results are not full, returns 1 if results are full
}

function ResetCheckCollision() {
    col_posx = 0;
    col_posy = 0;
    col_enemies = 0;
    col_allies = 0;
    col_terrain = 0;
    col_shape = 0;
    col_shape_size = 0;
    col_max_results = 0;
    for (var i = 0; i < 8; i++) col_results_unit[i] = 0;
    for (var i = 0; i < 8; i++) col_results_unit_activei[i] = 0;
    col_results_unit_i = 0;
    col_results_terrain = 0;
}

function CheckCollision(playerID) {
    // checks for things according to the configuration of the above vars. use results vars for the results of the check.
    // need to make this check for alliance status
    var result_exists = 0;
    var check_from = 0;
    var check_to = 0;
    if (playerID >= 0 && playerID <= 5) {
        check_from = playerID * ACTIVE_UNITS_MAX;
        check_to = check_from + ACTIVE_UNITS_MAX;
    } else {
        check_to = ACTIVE_UNITS_MAX;
    }
    if (col_shape == 0) {
        const l, r, t, b = PosToSquare(col_posx, col_posy, col_shape_size);
        for (var u = check_from; u < check_to; u++) {
            if (active_units[u] > 0) {
                const unit = CUnit(active_units[u]);
                if (col_enemies == 0 && !isAlly(playerID, unit.playerID)) continue;
                if (col_allies == 0 && isAlly(playerID, unit.playerID)) continue;
                if (unit.hp > 0 && IsPosWithinRect(unit.posX, unit.posY, l, r, t, b)) {
                    result_exists = 1;
                    const full = AddUnitToColResults(active_units[u], u);
                    if (full) break;
                }
            }
        }
    } else if (col_shape == 1) {
        for (var u = check_from; u < check_to; u++) {
            if (active_units[u] > 0) {
                const unit = CUnit(active_units[u]);
                if (unit.hp > 0 && GetDistance(unit.posX, unit.posY, col_posx, col_posy) <= col_shape_size) {
                    result_exists = 1;
                    const full = AddUnitToColResults(active_units[u], u);
                    if (full) break;
                }
            }
        }
    }
    return result_exists;
}

const EVENT_POOL = 192;
const event_posx = EUDArray(EVENT_POOL);
const event_posy = EUDArray(EVENT_POOL);
const event_time = EUDArray(EVENT_POOL);
const event_maxtime = EUDArray(EVENT_POOL);
const event_size = EUDArray(EVENT_POOL);
const event_shape = EUDArray(EVENT_POOL);
const event_heal = EUDArray(EVENT_POOL);
const event_damage = EUDArray(EVENT_POOL);
const event_owner = EUDArray(EVENT_POOL);
const event_maxtargets = EUDArray(EVENT_POOL);
const event_affect_enemies = EUDArray(EVENT_POOL);
const event_affect_allies = EUDArray(EVENT_POOL);
function ResetEvent(i) {
    event_posx[i] = 0;
    event_posy[i] = 0;
    event_time[i] = 0;
    event_maxtime[i] = 0;
    event_size[i] = 0;
    event_shape[i] = 0;
    event_heal[i] = 0;
    event_damage[i] = 0;
    event_owner[i] = 0;
    event_maxtargets[i] = 0;
    event_affect_enemies[i] = 0;
    event_affect_allies[i] = 0;
}

var queue_event_posx = 0;
var queue_event_posy = 0;
var queue_event_time = 0;
var queue_event_maxtime = 0;
var queue_event_size = 0;
var queue_event_shape = 0;
var queue_event_heal = 0;
var queue_event_damage = 0;
var queue_event_owner = 0;
var queue_event_maxtargets = 0;
var queue_event_affect_enemies = 0;
var queue_event_affect_allies = 0;
function ResetEventQueue() {
    queue_event_posx = 0;
    queue_event_posy = 0;
    queue_event_time = 0;
    queue_event_maxtime = 0;
    queue_event_size = 0;
    queue_event_shape = 0;
    queue_event_heal = 0;
    queue_event_damage = 0;
    queue_event_owner = 0;
    queue_event_maxtargets = 0;
    queue_event_affect_enemies = 0;
    queue_event_affect_allies = 0;
}

function CreateEvent() {
    for (var i = 0; i < EVENT_POOL; i++) {
        if (event_time[i] == 0) {
            event_time[i] = 1;
            event_posx[i] = queue_event_posx;
            event_posy[i] = queue_event_posy;
            event_maxtime[i] = queue_event_maxtime;
            event_size[i] = queue_event_size;
            event_shape[i] = queue_event_shape;
            event_heal[i] = queue_event_heal;
            event_damage[i] = queue_event_damage;
            event_owner[i] = queue_event_owner;
            event_maxtargets[i] = queue_event_maxtargets;
            event_affect_enemies[i] = queue_event_affect_enemies;
            event_affect_allies[i] = queue_event_affect_allies;
            ResetEventQueue();
            break;
        }
    }
}

function UpdateEvents() {
    // TODO: this needs to accomodate healing units and needs to affect players
    for (var i = 0; i < EVENT_POOL; i++) {
        if (event_time[i] > 0) {
            const playerID = event_owner[i];
            col_posx = event_posx[i];
            col_posy = event_posy[i];
            col_shape_size = event_size[i];
            col_max_results = event_maxtargets[i]; 
            col_enemies = event_affect_enemies[i];
            col_allies = event_affect_allies[i];
            const result_exists = CheckCollision(playerID);
            if (result_exists) {
                for (var result_i = 0; result_i < col_max_results; result_i++) {
                    if (col_results_unit[result_i] > 0) {
                        const target = CUnit(col_results_unit[result_i]);
                        if (event_damage[i] > 0) DamageUnit(playerID, target, event_damage[i], col_results_unit_activei[result_i]);
                    }
                }
            }
            ResetCheckCollision();
            event_time[i]++;
            if (event_time[i] >= event_maxtime[i]) {
                ResetEvent(i);
            }
        }
    }
}

function UpdateTime() {
    time_state_changed = 0;
    time += 1;
    total_time += 1;
    if (time > FULL_DAY_LENGTH) {
        time = 0; 
        time_state = 0;
        time_state_changed = 1;
        days_elapsed += 1;
        DisplayTextAll("\x13\x03--+ The Sun Rises +--");
    } else if (time_state == 0 && time >= FULL_DAY_LENGTH/2) {
        time_state = 1;
        time_state_changed = 1;
        DisplayTextAll("\x13\x1C--+ The Sun Sets +--");
    }
}

function DoDamageAtPos(positionx, positiony, playerID, shape, size, damage, maxtargets) {
    queue_event_owner = playerID;
    queue_event_posx = positionx;
    queue_event_posy = positiony;
    queue_event_shape = shape;
    queue_event_size = size;
    queue_event_damage = damage;
    queue_event_affect_enemies = 1;
    queue_event_maxtargets = maxtargets;
    queue_event_maxtime = 1;
    CreateEvent();
}

var fow_state;
function UpdateFOW() {
    if (fow_state == 0) {
        fow_state = 1;
        SetMemory(0x0051CE98, SetTo, 0);
    } else {
        fow_state = 0;
        SetMemory(0x0051CE98, SetTo, 1);
    }
}

function UpdateSigils() {
    for (var i = 0; i < MAX_SIGIL_SPAWNS; i++) {
        if (sigil_x[i] > 0) {
            var player_on_sigil = 0;
            for (var p = 0; p < 6; p++) {
                if (p_current_sigil[p] == i && p_on_sigil[p] > 0) {
                    player_on_sigil = 1;
                    break;
                }
            }
            if (player_on_sigil) {
                sigil_glow[i]++;
                if (sigil_glow[i] >= 24) {
                    DrawCircle(sigil_x[i], sigil_y[i], SIGIL_DIAMETER, 24, 443);
                    sigil_glow[i] = 0;
                }
            } else sigil_glow[i] = 0;
        }
    }
}

function UpdateBrightness(playerID) {
    var target_brightness = BRIGHTNESS_DEFAULT;
    if (time_state == 1) target_brightness = BRIGHTNESS_NIGHT;
    if (p_indoors[playerID]) target_brightness -= 6;
    if (p_brightness[playerID] < target_brightness) p_brightness[playerID] += 1;
    else if (p_brightness[playerID] > target_brightness) p_brightness[playerID] -= 1;
    if (IsUserCP()) SetMemoryEPD(EPD(0x657A9C), SetTo, p_brightness[playerID]);
}

function UpdateSightRange(playerID) {
    var target_value = PLAYER_DEFAULT_SIGHT;
    if (time_state == 1) target_value = PLAYER_DEFAULT_SIGHT - 4;
    if (p_indoors[playerID] && time_state == 0) target_value = 2;
    else if (p_indoors[playerID]) target_value = 1;
    bwrite(0x65FD00 + 13624 + U_HERO[playerID] * 1, target_value);
}

function UpdateSigilMenu(playerID) {
    if (keypress_1[playerID] && p_attributepoints[playerID] > 0) {
        p_strength[playerID]++;
        p_attributepoints[playerID]--;
        CalculateKnightSwordDamage(playerID);
        for (var i = 0; i < 8; i++) {
            queue_pro_posx = p_x[playerID];
            queue_pro_posy = p_y[playerID];
            queue_pro_maxtime = 5;
            queue_pro_angle = (i + 1) * 45;
            queue_pro_velocity = 20;
            queue_pro_effect2 = 450;
            queue_pro_effect2_start = 3;
            CreateProjectile();
        }
        PlayWAV("staredit/wav/increase_attribute.ogg");
        PlayWAV("sound/Bullet/ZLrkHit2.wav");
    }
    if (keypress_2[playerID] && p_attributepoints[playerID] > 0) {
        p_dexterity[playerID]++;
        p_attributepoints[playerID]--;
        for (var i = 0; i < 8; i++) {
            queue_pro_posx = p_x[playerID];
            queue_pro_posy = p_y[playerID];
            queue_pro_maxtime = 5;
            queue_pro_angle = (i + 1) * 45;
            queue_pro_velocity = 20;
            queue_pro_effect2 = 505;
            queue_pro_effect2_start = 3;
            CreateProjectile();
        }
        PlayWAV("staredit/wav/increase_attribute.ogg");
        PlayWAV("sound/Bullet/ZLrkHit2.wav");
    }
    if (keypress_3[playerID] && p_attributepoints[playerID] > 0) {
        p_resilience[playerID]++;
        p_attributepoints[playerID]--;
        for (var i = 0; i < 8; i++) {
            queue_pro_posx = p_x[playerID];
            queue_pro_posy = p_y[playerID];
            queue_pro_maxtime = 5;
            queue_pro_angle = (i + 1) * 45;
            queue_pro_velocity = 20;
            queue_pro_effect2 = 466;
            queue_pro_effect2_start = 3;
            CreateProjectile();
        }
        PlayWAV("staredit/wav/increase_attribute.ogg");
        PlayWAV("sound/Bullet/ZLrkHit2.wav");
    }
    screen.printfAt(0, "\x09\x09\x09\x11.STRENGTH \x1F({})\x09\x09\x09\x09\x07.DEXTERITY \x1F({})\x09\x09\x09\x09\x1C.RESILIENCE \x1F({})", p_strength[playerID], p_dexterity[playerID], p_resilience[playerID]);
    DisplayTextAt(1, "\x09\x09\x09\x03.Press -<1>- to Allocate\x09\x09\x03.Press -<2>- to Allocate\x09\x09\x03.Press -<3>- to Allocate");
    DisplayTextAt(2, "\x09\x09\x09\x11|\x04Sword Effectiveness\x09\x09\x07|\x04Bow Effectiveness\x09\x09\x09\x1C|\x04Magic Effectiveness");
    DisplayTextAt(3, "\x09\x09\x09\x11|\x04Life Regeneration\x09\x09\x09\x07|\x04Life Toughness\x09\x09\x09\x09\x1C|\x04Armor Toughness");
    DisplayTextAt(4, "\x09\x09\x09\x11|\x04Something\x09\x09\x09\x09\x09\x07|\x04Attack Speed\x09\x09\x09\x09\x1C|\x04Life Drain Recovery");
    DisplayTextAt(5, "\x0BNull");
    screen.printfAt(6, "\x09\x09\x09\x04Available Attribute Points: \x1F{}", p_attributepoints[playerID]);
}

function ResetWeaponIndex(index) {
    weapon_cunit[index] = 0;
    weapon[index] = 0;
    weapon_state[index] = 0;
    weapon_cooldown[index] = 0;
    weapon_angle[index] = 0;
    weapon_x[index] = 0;
    weapon_y[index] = 0;
}
// @bm UpdateWeaponState
function UpdateWeaponState(playerID) {
    var check_from = 0;
    var check_to = 0;
    if (playerID >= 0 && playerID <= 5) {
        check_from = playerID;
        check_to = playerID + 1;
    } else if (playerID == $P7) {
        check_from = 6;
        check_to = MAX_WEAPON_OBJECTS;
    }
    for (var weapon_index = check_from; weapon_index < check_to; weapon_index++) {
        const i = weapon_index;
        if (weapon[i] > 0) {
            var unit_ptr = weapon_cunit[i]; // set unit to whatever the weapon id is attached to
            var is_player = 0;
            var unit_just_attacked = 0;
            var unit_x = 0;
            var unit_y = 0;
            var unit_order_x = 0;
            var unit_order_y = 0;
            var unit_angle = 0;
            if (i >= 0 && i <= 5) {unit_ptr = p_cunit[i]; is_player = 1;} // set unit to players unit if its a player
            const unit = CUnit(unit_ptr);
            if (is_player) {
                unit_just_attacked = p_just_attacked[i];
                unit_x = p_x[i];
                unit_y = p_y[i];
                unit_order_x = p_orderx[i];
                unit_order_y = p_ordery[i];
                unit_angle = p_angle[i];
            } else if (playerID == $P7) {
                if (unit.hp == 0) {
                    ResetWeaponIndex(i);
                    continue;
                }
                unit_x = unit.posX;
                unit_y = unit.posY;
                unit_order_x = unit.orderTargetX;
                unit_order_y = unit.orderTargetY;
                unit_angle = SCDirectionToDeg(unit.currentDirection1);
            }

            // TRIGGER
            if (weapon_cooldown[i] > 0) weapon_cooldown[i]--;
            var weapon_triggered = CheckForWeaponTrigger(playerID, unit, weapon[i]);
            if (weapon_triggered && weapon_cooldown[i] == 0) {
                weapon_cooldown[i] = GetWeaponCooldown(weapon[i]);
                weapon_state[i] = 1;
            }

            // SHORT SWORD
            if (weapon_state[i] > 0 && weapon[i] == WEAPON_SHORTSWORD) {
                if (weapon_state[i] == 1) {
                    const damage = GetWeaponDamage(weapon[i], weapon_state[i], playerID);
                    weapon_x[i], weapon_y[i] = MovePosTowards(unit_x, unit_y, unit_angle, 32);
                    DoDamageAtPos(weapon_x[i], weapon_y[i], playerID, 1, 128, damage, 2);
                    weapon_angle[i] = unit_angle + 60;
                    PlaySword();
                }
                if (weapon_state[i] > 0) {
                    queue_pro_posx = unit_x;
                    queue_pro_posy = unit_y;
                    queue_pro_maxtime = 3;
                    queue_pro_angle = weapon_angle[i];
                    queue_pro_velocity = 20;
                    queue_pro_effect1_type = 0;
                    queue_pro_effect1 = 424;
                    queue_pro_effect1_start = 0;
                    queue_pro_effect2_type = 0;
                    queue_pro_effect2 = 213;
                    queue_pro_effect2_start = 3;
                    CreateProjectile();
                    weapon_angle[i] -= 20;
                }
                weapon_state[i]++;
                if (weapon_state[i] >= 6) weapon_state[i] = 0;
            }
            // FIRE BALL
            if (weapon_state[i] > 0 && weapon[i] == WEAPON_FIREBALL) {
                if (weapon_state[i] == 1) {
                    const angle_to_order = GetAngle(unit_x, unit_y, unit_order_x, unit_order_y);
                    const distance_to_order = GetDistance(unit_x, unit_y, unit_order_x, unit_order_y);
                    if (distance_to_order > 256) weapon_x[i], weapon_y[i] = MovePosTowards(unit_x, unit_y, angle_to_order, 256);
                    else weapon_x[i], weapon_y[i] = unit_order_x, unit_order_y;
                    queue_pro_posx, queue_pro_posy = MovePosTowards(weapon_x[i], weapon_y[i], 280, 366);
                    queue_pro_maxtime = 20;
                    queue_pro_angle = 100;
                    queue_pro_velocity = 20;
                    queue_pro_effect1 = 332;
                    queue_pro_effect1_start = 0;
                    queue_pro_effect2 = 333;
                    queue_pro_effect2_start = queue_pro_maxtime;
                    CreateProjectile();
                    PlayWAV("staredit/wav/fireball_incoming.ogg");
                }
                weapon_state[i]++;
                if (weapon_state[i] >= 20) {
                    weapon_state[i] = 0;
                    const damage = GetWeaponDamage(weapon[i], weapon_state[i], playerID);
                    DoDamageAtPos(weapon_x[i], weapon_y[i], playerID, 1, 128, damage, 8);
                    setloc(L_MAIN, weapon_x[i], weapon_y[i]);
                    CreateImageSpriteAtLocation($P8, L_MAIN, 429, 0);
                    PlayWAV("staredit/wav/fireball.ogg");
                }
            }
        }
    }
}

function LevelUp(playerID, unit: CUnit) {
    p_level_current[playerID] += 1;
    p_attributepoints[playerID] += 1;
    p_exp_current[playerID] = 0;
    p_exp_goal[playerID] = 300 + 150 * p_level_current[playerID];
    unit.killCount = p_level_current[playerID];
    screen.printf("LEVEL UP - Level {}", p_level_current[playerID]);
    PlayWAV("staredit/wav/levelup.ogg");
    CreateUnitDeathAtLocation($U("Protoss Observer"), $P8, L_MAIN);
    for (var i = 0; i < 8; i++) {
        queue_pro_posx = p_x[playerID];
        queue_pro_posy = p_y[playerID];
        queue_pro_maxtime = 8;
        queue_pro_angle = (i + 1) * 45;
        queue_pro_velocity = 40;
        queue_pro_angular_velocity = Random(-10, 10);
        queue_pro_effect0 = U_KAKARU;
        queue_pro_effect0_type = 1;
        queue_pro_effect1 = 213;
        queue_pro_effect1_start = queue_pro_maxtime - 1;
        queue_pro_effect2 = 427;
        queue_pro_effect2_start = queue_pro_maxtime;
        CreateProjectile();
    }
}

function FillActiveNearbyUnits(playerID, positionx, positiony) {
    var storage_i = (playerID * ACTIVE_UNITS_MAX) + 1;
    const storage_max = (playerID + 1) * ACTIVE_UNITS_MAX;
    for (var i = storage_i; i < storage_max; i++) active_units[i] = 0;
    const limit_left, limit_right, limit_top, limit_bottom = PosToSquare(positionx, positiony, ACTIVE_UNITS_RECT);
    if (IsAlive(playerID)) active_units[storage_i - 1] = p_cunit[playerID]; // set the first storage index for the player to the player's unit.
    var i = 0;
    foreach(unit : EUDLoopCUnit()) {
        if (unit.playerID == $P7 && unit.hp > 0) {
            if (IsPosWithinRect(unit.posX, unit.posY, limit_left, limit_right, limit_top, limit_bottom)) { // check if unit is within square, if it is add it to the active units array.
                active_units[storage_i] = unit;
                storage_i++;
                if (storage_i > storage_max) break;
            }
        }
        i++;
        if (i > 1000) {
            DisplayTextAll("NOTICE: CHECKED MORE THAN 1000 UNITS!");
            break;
        }
    }
}

// @bm onPluginStart
function onPluginStart() {
    SetupStringArrays();
    StoreSpawnPositions();
    SpawnInitialItemUnits();
    EUDPlayerLoop()();
        const playerID = getcurpl();
        if (playerID >= 0 && playerID < 6) {
            for (var i = 0; i < 6; i++) SetAllianceStatus(i, Ally);
            SetAllianceStatus($P7, Enemy);
            SetAllianceStatus($P8, Ally);
            SetHeroDefaults(playerID);
            if (debug_enabled) {
                p_attributepoints[playerID] = 20;
                EquipItem(ARMOR_SHATTERED_KNIGHTS, U_ARMOR, playerID);
                EquipItem(WEAPON_FIREBALL, U_WEAPON, playerID);
            }
            PlaySong(0);
            // GetVision($P7);
        }
    EUDEndPlayerLoop();
}
// @bm afterTriggerExec
function afterTriggerExec() {
    UpdateTime();
    UpdateProjectiles();
    UpdateEvents();
    UpdateSigils();
    // UpdateFOW();
    //reset chunk spawn amount
    if (time_state_changed && time_state == 0) {
        var reset_count = 0;
        for (var i = 0; i < 256; i++) {
            if (chunk_spawns[i] > 0) {
                chunk_spawns[i] = 0;
                reset_count += 1;
            }
        }
        screen.printf("reset spawns for {} chunks", reset_count);
    }
    if (test_delay > 0) test_delay -= 1;
    EUDPlayerLoop()();
        const playerID = getcurpl();
        // HUMAN PLAYERS
        if (playerID >= 0 && playerID < 6) {
            const unit = CUnit(p_cunit[playerID]);
            var order_input = 0;
            var unit_is_moving = 0;
            p_just_attacked[playerID] = 0;
            var unit_is_indoors = 0;
            var unit_took_health_damage = 0;
            if (IsAlive(playerID)) {
                p_x[playerID], p_y[playerID] = unit.posX, unit.posY;
                p_angle[playerID] = SCDirectionToDeg(unit.currentDirection1);
                p_orderx[playerID], p_ordery[playerID] = unit.orderTargetX, unit.orderTargetY;

                if (active_units_update == 0) FillActiveNearbyUnits(playerID, p_x[playerID], p_y[playerID]);
                if (unit.orderID == ORDER_HOLD || unit.orderID == ORDER_PATROL) {
                    order_input = unit.orderID;
                    unit.orderID = 3;
                }
                if (p_x[playerID] == p_x_old[playerID] && p_y[playerID] == p_y_old[playerID]) {
                    unit_is_moving = 0;
                } else {
                    p_x_old[playerID] = p_x[playerID];
                    p_y_old[playerID] = p_y[playerID];
                    unit_is_moving = 1;
                }
                if (!unit_is_moving && unit.currentSpeed1 > 0) {
                    unit.currentSpeed1 = 0; // Unit stuck because of flingy movement fix.
                    unit.currentSpeed2 = 0;
                }
            }
            if (unit_is_moving && IsAlive(playerID)) {
                const x, y = GetChunkAtPos(p_x[playerID], p_y[playerID]);
                if (p_chunk_x_old[playerID] != x || p_chunk_y_old[playerID] != y) {
                    p_chunk_x_old[playerID] = x;
                    p_chunk_y_old[playerID] = y;
                    JustEnteredChunk(x, y);
                }
                p_indoors[playerID] = IsPosIndoors(p_x[playerID], p_y[playerID]);
                var sigil = MAX_SIGIL_SPAWNS;
                for (var i = 0; i < MAX_SIGIL_SPAWNS; i++) {
                    if (sigil_x[i] > 0) {
                        const dist = GetDistance(p_x[playerID], p_y[playerID], sigil_x[i], sigil_y[i]);
                        if (dist <= SIGIL_DIAMETER / 2) {
                            sigil = i;
                            break;
                        }
                    }
                }
                if (sigil < MAX_SIGIL_SPAWNS && p_on_sigil[playerID] == 0) {
                    p_on_sigil[playerID] = 1;
                    p_current_sigil[playerID] = sigil;
                    DrawCircle(sigil_x[sigil], sigil_y[sigil], SIGIL_DIAMETER, 24, 443);
                    DisplayText("\x13\x04Press \x03-<F>- \x04to Interact.");
                } else if (sigil >= MAX_SIGIL_SPAWNS && p_on_sigil[playerID] > 0) {
                    p_on_sigil[playerID] = 0;
                }
                
            }
            UpdateBrightness(playerID);
            UpdateSightRange(playerID);

            // SIGIL INTERACTION
            if (p_on_sigil[playerID] && IsAlive(playerID)) {
                if (m_main[playerID] == 0 && keypress_f[playerID] > 0) {
                    m_main[playerID] = MENU_MAIN_SIGIL;
                } else if (m_main[playerID] == MENU_MAIN_SIGIL) {
                    UpdateSigilMenu(playerID);
                }
            } else if (p_on_sigil[playerID] == 0 && m_main[playerID] > 0) {
                m_main[playerID] = 0;
                ClearTextLines(0, 9);
            }

            // HEALTH
            if (IsAlive(playerID)) {
                const regen_amount = GetHealthRegen(playerID);
                HealPlayer(playerID, unit, regen_amount); // regen from strength
                if (unit.hp > p_health[playerID]) {
                    if (unit.hp > p_health_max[playerID]) unit.hp = p_health_max[playerID];
                    const health_gained = unit.hp - p_health[playerID];
                    p_health[playerID] = unit.hp;
                    // const gained_whole, gained_frac = GetWholeAndFraction((health_gained * 10000) / 256);
                    // screen.printf("{}.{} health gained", gained_whole, gained_frac);
                }
                if (unit.hp < p_health[playerID]) {
                    const health_lost = p_health[playerID] - unit.hp;
                    unit_took_health_damage = health_lost;
                    const amount_recovered = GetHealthToughnessDeduction(playerID, health_lost);
                    HealPlayer(playerID, unit, amount_recovered); // toughness from dexterity
                    p_health[playerID] = unit.hp;
                    // const recovered_whole, recovered_frac = GetWholeAndFraction((amount_recovered * 10000) / 256);
                    // screen.printf("{} health LOST! Recovered {}.{} from toughness.", health_lost / 256, recovered_whole, recovered_frac);
                }
            }

            // SHIELDS/ARMOR
            if (IsAlive(playerID)) {
                CenterLocHero(L_16x16, playerID);
                if (unit.shield != p_shield_max[playerID] && !EnemiesAtLoc(L_16x16)) HealShieldPlayer(playerID, unit, p_shield_max[playerID]);
                CenterLocHero(L_3X3, playerID);
                if (EnemiesAtLoc(L_3X3) && p_shield_recover[playerID] == 0) p_shield_recover[playerID] = 1;
                else if (!EnemiesAtLoc(L_3X3) && p_shield_recover[playerID] > 0) {
                    p_shield_recover[playerID] = 0;
                    HealShieldPlayer(playerID, unit, p_shield_max[playerID]);
                    CreateImageSpriteAtLocation($P8, L_3X3, 377, 0);
                    PlayWAV("staredit/wav/dodge.ogg");
                }
                if (unit.shield == p_shield_max[playerID] && p_shield_broken[playerID] > 0) p_shield_broken[playerID] = 0;
                else if (p_shield_broken[playerID] > 0) unit.shield = 0;
                if (unit.shield > p_shield[playerID]) {
                    if (unit.shield > p_shield_max[playerID]) unit.shield = p_shield_max[playerID];
                    const armor_gained = unit.shield - p_shield[playerID];
                    p_shield[playerID] = unit.shield;
                    // const gained_whole, gained_frac = GetWholeAndFraction((armor_gained * 10000) / 256);
                    // screen.printf("{}.{} armor gained", gained_whole, gained_frac);
                }
                if (unit.shield < p_shield[playerID]) {
                    const armor_lost = p_shield[playerID] - unit.shield;
                    const amount_recovered = GetArmorToughnessDeduction(playerID, armor_lost);
                    HealShieldPlayer(playerID, unit, amount_recovered); // toughness from resilience
                    if (unit_took_health_damage && p_shield[playerID] >= 256) {
                        p_shield_broken[playerID] = 1;
                        // DisplayText("\x13\x06-! ARMOR BROKEN !-");
                        PlayWAV("staredit/wav/shields_broke.ogg");
                        setloc(L_MAIN, p_x[playerID], p_y[playerID]);
                        CreateImageSpriteAtLocation($P8, L_MAIN, 107, 16);
                    }
                    p_shield[playerID] = unit.shield;
                    // screen.printf("{} armor LOST", armor_lost / 256);
                }
            }

            // UNIT ATTACK
            if (IsAlive(playerID)) {
                if (unit.groundWeaponCooldown == 26) {
                    p_just_attacked[playerID] = 1;
                    unit.groundWeaponCooldown -= GetAttackSpeed(playerID);
                    const target = unit.orderTargetUnit;
                    var target_is_valid = 1;
                    if (target.unitID == U_WEAPON || target.unitID == U_ARMOR) {
                        unit.orderID = 3;
                        p_just_attacked[playerID] = 0;
                        CenterLocHero(L_16x16, playerID);
                        ModifyUnitHitPoints(All, target.unitID, $P8, L_16x16, 100);
                        target_is_valid = 0;
                        if (UnitItemHasBeenLooted(playerID, target.shield)) {
                            DisplayText("You've already looted this.");
                        } else {
                            p_current_item_unit[playerID] = target;
                            PlayWAV("staredit/wav/check_item.ogg");
                        }
                    }
                    if (target_is_valid) DamageUnit(playerID, target, p_damage[playerID], ACTIVE_UNITS_MAX + 1);
                }
            }

            // LOOT MENU
            if (p_current_item_unit[playerID] > 0) {
                const item_unit = CUnit(p_current_item_unit[playerID]);
                const item_id = item_unit.energy;
                const item_looted_status = item_unit.shield;
                const item_unit_type = item_unit.unitID;
                const item_x, item_y = item_unit.posX, item_unit.posY;
                if (GetDistance(p_x[playerID], p_y[playerID], item_x, item_y) > CLOSE_LOOT_MENU_DISTANCE) {
                    ClearTextLines(0, 2);
                    p_current_item_unit[playerID] = 0;
                } else {
                    const item_name = GetStringItemName(item_id, item_unit_type);
                    const item_description = GetStringItemDescription(item_id, item_unit_type);
                    var equipped_id = 0; // whatever is currently equipped in the target slot
                    if (item_unit_type == U_WEAPON) equipped_id = weapon[playerID];
                    else if (item_unit_type == U_ARMOR) equipped_id = p_armor[playerID];
                    const equipped_name = GetStringItemName(equipped_id, item_unit_type);
                    screen.printfAt(0, "\x09\x09\x09\x09\x09\x09\x09\x09\x09\x04--+ \x1F{:s}", item_name);
                    screen.printfAt(1, "\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x02{:s}", item_description);
                    if (equipped_id > 0) {
                        screen.printfAt(2, "\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x02{:s}\x1F{:s}", strings_loot_interactions[1], equipped_name);
                    } else {
                        screen.printfAt(2, "\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x09\x02{:s}", strings_loot_interactions[0]);
                    }
                    if (keypress_1[playerID]) {
                        item_unit.shield = ModifyItemLootedStatus(playerID, item_looted_status);
                        if (item_unit.shield == 0) { // remove the unit if the loot status returns 0
                            setloc(L_MAIN, item_x, item_y);
                            RemoveUnitAt(1, item_unit_type, L_MAIN, $P8);
                        }
                        if (equipped_id > 0) {
                            setloc(L_MAIN, p_x[playerID], p_y[playerID]);
                            CreateItemUnit(item_unit_type, equipped_id, L_MAIN);
                            screen.printf("\x13\x06Dropped \x1F{:s}", equipped_name);
                        }
                        EquipItem(item_id, item_unit_type, playerID);
                        p_current_item_unit[playerID] = 0;
                        ClearTextLines(0, 2);
                        screen.printf("\x13\x04Equipped \x1F{:s}", item_name);
                        PlayWAV("staredit/wav/equip_weapon.ogg");
                    }
                }
            }

            // WEAPON
            UpdateWeaponState(playerID);

            // EXP
            if (p_exp_buffer[playerID] > 0) {
                var exp_just_gained = p_exp_buffer[playerID];
                p_exp_total[playerID] += exp_just_gained;
                p_exp_current[playerID] += exp_just_gained;
                if (p_exp_current[playerID] >= p_exp_goal[playerID]) {
                    exp_just_gained = p_exp_current[playerID] - p_exp_goal[playerID];
                    p_exp_current[playerID] += exp_just_gained;
                    LevelUp(playerID, p_cunit[playerID]);
                }
                screen.printf("\x12\x1F+{} Exp \x04({} | {})\x09\x09", p_exp_buffer[playerID], p_exp_current[playerID], p_exp_goal[playerID]);
                PlayWAV("staredit/wav/gain_exp.ogg");
                p_exp_buffer[playerID] = 0;
            }

            // TIME
            if (time_state_changed) {
                if (time_state == 0) {
                    PlaySong(0);
                }
                else if (time_state == 1) {
                    PlayWAV("staredit/wav/night_ambience.ogg");
                }
            }

            // RESPAWN
            if (!IsAlive(playerID) && total_time > 144 && !debug_enabled) {
                SpawnHero(playerID);
            } else if (!IsAlive(playerID) && debug_enabled) {
                SpawnHero(playerID);
            }

            if (debug_enabled) {
                if (p_on_sigil[playerID]) {
                    HealPlayer(playerID, unit, 256);
                }
                if (test_delay == 0 && playerID == 0) {
                    test_delay = 12;
                }
                debug_values[0] = p_x[playerID];
                debug_values[1] = p_y[playerID];
                debug_values[2] = unit.orderID;
                eprintf("{} | {} | {} | {} | {} | {} | {} | {}", debug_values[0], debug_values[1], debug_values[2], debug_values[3], debug_values[4], debug_values[5], debug_values[6], debug_values[7]);
            }
        }

        // PLAYER 7 - MONSTERS
        if (playerID == $P7) {
            UpdateWeaponState(playerID);
        }
    EUDEndPlayerLoop();

    if (!debug_enabled) {
        if (total_time == 48) {
            ClearTextLines(0, 10);
            DisplayTextAllAt(5, "\x13\x06SPIRIT KNIGHTS");
        }
        if (total_time == 120) {
            ClearTextLines(0, 10);
            DisplayTextAllAt(5, "\x13\x04You wake up.");
        }
    }

    if (active_units_update == 0) active_units_update = 24;
    active_units_update -= 1;
}